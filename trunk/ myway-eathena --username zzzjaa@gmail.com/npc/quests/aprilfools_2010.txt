//===== eAthena Script =======================================
//= kRO 2010 April Fools Event
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN 14200+
//===== Description: =========================================
//= Chance to obtain Chocolate Donut In Mouth lower headgear.
//= Allows Geek Glasses to be enchanted.
//===== Additional Comments: =================================
//= 1.0 First version. [Chilly]
//============================================================

geffen,130,204,4	script	Bad Broker#01::aprilfools	870,{
	if (fakevalkyrie < 1) {
		mes "[Bad Broker]";
		mes "You there, got a minute?";
		next;
		mes "[Bad Broker]";
		mes "I need your help with something... but you have to promise not to tell anyone.";
		next;
		mes "[Bad Broker]";
		mes "I made tons of ^0000FFFake Valkyrie Cards^000000 for April Fool's day that I was planning to sell.  I made enough to become a billionare in no time.";
		next;
		mes "[Bad Broker]";
		mes "The problem is, while I was traveling to Prontera I was attacked by a real ^0000FFValkyrie^000000!";
		next;
		mes "[Bad Broker]";
		mes "My bag was ripped open and the Fake Valkyrie Cards were scattered all over the place.";
		next;
		set fakevalkyrie,1;
		mes "[Bad Broker]";
		mes "Please find as many of these cards as possible and bring them to the real ^0000FFValkyrie in Prontera^000000.  Also, tell her I'll never make fake cards again.";
		close;
	}
	else {
		mes "[Bad Broker]";
		mes "Sorry for the trouble, but can you please find as many ^0000FFFake Valkyrie Cards^000000 as you can and bring them to the real ^0000FFValkyrie in Prontera^000000?";
		close;
	}
}

alberta,127,85,4	duplicate(aprilfools)	Bad Broker#02	870
payon,125,89,4	duplicate(aprilfools)	Bad Broker#03	870

prontera,250,113,6	script	Real Valkyrie	811,{
	if (fakevalkyrie < 1) {
		mes "[Real Valkyrie]";
		mes "Hello adventurer, have you seen anyone trying to sell ^0000FFFake Valkyrie Cards^000000 anywhere?";
		next;
		mes "[Real Valkyrie]";
		mes "If you have please let me know, I will reward you of course.";
		close;
	}
	else if (fakevalkyrie == 1) {
		if ((countitem(4454) < 20) || (countitem(4455) < 20)) {
			mes "[Real Valkyrie]";
			mes "Oh?  You found the person making the Fake Valkyrie Cards?";
			next;
			mes "[Real Valkyrie]";
			mes "Good job!  Now can you please bring me as many of these fake cards as possible?";
			close;
		}
		else {
			mes "[Real Valkyrie]";
			mes "Oh?  You found the person making the Fake Valkyrie Cards?";
			next;
			mes "[Real Valkyrie]";
			mes "Good job!  I see you have many of them with you now.  Can I take a look at one?";
			next;
			delitem 4455,1; //Fake Valkyrie Card
			mes "[Real Valkyrie]";
			mes "Hmm, what is this?  The picture of me is obviously wrong.  I can't believe someone would do such a poor job of making fake cards.";
			next;
			mes "[Real Valkyrie]";
			mes "Thank you for showing me this, now let me reward you with a ^0000FFChocolate Donut^000000.  I love donuts but I'm still learning how to make them, please forgive me if I burn it~";
			next;
			mes "-Casts a fire spell on a donut-";
			progressbar "ffff00",4;
			if (rand(1,3) == 3) {
				getitem 12546,5; //Winning Cuisine
				set fakevalkyrie,2;
				mes "[Real Valkyrie]";
				mes "Failure!  Sorry, I burned the donut this time~";
				next;
				mes "[Real Valkyrie]";
				mes "As a bonus, for every ^0000FF20 Fake Valkyrie Cards^000000 you bring me I'll enchant a pair of ^0000FFGeek Glasses^000000 for you.";
				close;

			}
			else {
				getitem 5775,1; //Chocolate Donut In Mouth
				set fakevalkyrie,2;
				mes "[Real Valkyrie]";
				mes "Success!  I hope you enjoy it~";
				next;
				mes "[Real Valkyrie]";
				mes "As a bonus, for every ^0000FF20 Fake Valkyrie Cards^000000 you bring me I'll enchant a pair of ^0000FFGeek Glasses^000000 for you.";
				close;
			}
		}
	}
	else if (fakevalkyrie == 2) {
		if (countitem(2243) < 1) {
			mes "[Real Valkyrie]";
			mes "You must bring me a pair of ^0000FFGeek Glasses^000000 if you want me to enchant them.";
			close;
		}
		else if ((countitem(4454) > 19) || (countitem(4455) > 19))  {
			if (countitem(2243) > 1) {
				mes "[Real Valkyrie]";
				mes "You have too many Geek Glasses with you, I can only enchant one at a time.";
				close;
			}
			else {

				mes "[Real Valkyrie]";
				mes "Ah, you have everything I need.  I can add ^0000FF+1 or +2 of any stat^000000 to your Geek Glasses, but there is a ^FF0000chance they could break^000000 too.  Would you like me to try it now?";
				next;
				switch(select("Yes please.","No thanks.")) {
				case 1:

					mes "[Real Valkyrie]";
					mes "Alright.  Give me a few seconds to enchant this for you.";
					next;
					mes "-Casts a magic spell on the geek glasses-";
					close2;
					specialeffect2 EF_MAPPILLAR;
					if (countitem(4454) > 19) {
						delitem 4454,20; //Fake Valkyrie Card
					}
					else {
						delitem 4455,20; //Fake Valkyrie Card
					}
					progressbar "ffff00",4;
					delitem 2243,1;
					set .@enc_glasses,rand(1,40);
					if (.@enc_glasses == 1) {
						set .@addpart,4701;
					}
					else if (.@enc_glasses == 2) {
						set .@addpart,4711;
					}
					else if (.@enc_glasses == 3) {
						set .@addpart,4721;
					}
					else if (.@enc_glasses == 4) {
						set .@addpart,4731;
					}
					else if (.@enc_glasses == 5) {
						set .@addpart,4741;
					}
					else if (.@enc_glasses == 6) {
						set .@addpart,4751;
					}
					else if ((.@enc_glasses > 6) && (.@enc_glasses < 11)) {
						set .@addpart,4700;
					}
					else if ((.@enc_glasses > 10) && (.@enc_glasses < 15)) {
						set .@addpart,4710;
					}
					else if ((.@enc_glasses > 14) && (.@enc_glasses < 19)) {
						set .@addpart,4720;
					}
					else if ((.@enc_glasses > 18) && (.@enc_glasses < 23)) {
						set .@addpart,4730;
					}
					else if ((.@enc_glasses > 22) && (.@enc_glasses < 27)) {
						set .@addpart,4740;
					}
					else if ((.@enc_glasses > 26) && (.@enc_glasses < 31)) {
						set .@addpart,4750;
					}
					else {
						specialeffect2 EF_PHARMACY_FAIL;
						mes "[Real Valkyrie]";
						mes "Forgive me.  I ^FF0000broke your Geek Glasses^000000 while trying to enchant them.  If you bring me another pair I'll try again.";
						close;
					}
					getitem2 2243, 1, 1, 0, 0, 0, 0, 0, .@addpart;
					end;
				case 2:
					mes "[Real Valkyrie]";
					mes "Come back if you change your mind~";
					close;
				}
			}
		}
		else {
			mes "[Real Valkyrie]";
			mes "If you want me to enchant Geek Glasses you must bring me a pair with atleast ^0000FF20 Fake Valkyrie Cards^000000.";
			close;
		}
	}
}
